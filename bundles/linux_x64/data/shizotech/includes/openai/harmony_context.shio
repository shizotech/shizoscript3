
#include "harmony_prompt"

//Helper function declarations
def agt_timestamp();
def create_harmony_context();

class harmony_context
{
	sys_prompt = "";
	prompt = "";
	effort = "medium";
	tools = 0;
	tools_builtin = "";
	
	__init__(uprompt)
	{
		system_prompt("Be helpful, concise and direct, try to use tools to retrieve missing or realtime information instead of guessing. Do not guess facts, look them up first.", gpt_oss_system_prompt);
		
		if(!uprompt.empty())
			user_prompt(uprompt);
		//Dont append Assistant prefill yet, as it is appended each step.
	}
	
	copy(
		copy_prompt = true,
		copy_settings = true
	)
	{
		//c = harmony_context(""); //TODO: make this work!
		c = create_harmony_context();
		
		if(copy_prompt)
			c.sys_prompt = sys_prompt;
			c.prompt = prompt;
		if(copy_settings)
			c.effort = effort;
			c.tools = tools;
			c.tools_builtin = tools_builtin;
		return c;
	}
	
	enable_builtin_tools()
	{
		tools_builtin = gpt_oss_builtin_tools;
	}
	
	disable_builtin_tools()
	{
		tools_builtin = "";
	}
	
	system_prompt(dev_msg, sys_msg="") {
		if(sys_msg.empty())
			sys_msg = gpt_oss_system_prompt;
		sys_prompt = "<|start|>system<|message|>" + sys_msg + "<|end|><|start|>developer<|message|>" + dev_msg + "{{tools}}<|end|>"; 
	}
	
	user_prompt(uprompt)						{ role("user"); message(uprompt); token("end"); }
	build_prompt() 								{ 
		_sys = sys_prompt;
		_sys = _sys.replace("{{current_date}}", agt_timestamp()); 
		_sys = _sys.replace("{{effort}}", effort); 
		_sys = _sys.replace("{{tools}}", build_tools_prompt()); 
		_sys = _sys.replace("{{builtins}}", tools_builtin); 
		return _sys + prompt; 
	}
	
	token(name) 				{ prompt += "<|" + name + "|>"; }
	message(txt)				{ token("message"); prompt += txt; }
	role(name) 					{ token("start"); prompt += name; }
	sys(role_name, msg)			{ role(role_name); token("message"); prompt += msg; token("end"); }

	remove_analysis()
	{
		search_start_id = 0;
		search_end_id = 0;
		
		for(1)
		{
			search_start_id = prompt.find("<|start|>assistant<|channel|>analysis<|message|>", search_start_id);
			if(search_start_id == -1)
				return 0;
			search_end_id = prompt.find("<|end|>", search_start_id);
			if(search_end_id < search_start_id)
				return 0;
			
			tmp_prompt = prompt.substr(0, search_start_id);
			tmp_prompt += prompt.substr(search_end_id + std.count("<|end|>"));
			prompt = tmp_prompt;
			search_start_id = search_end_id;
			
		}
	}
	
	turns()
	{
		occured = 0;
		start_id = 0;
		
		for(1)
		{
			start_id = prompt.find("<|start|>user", start_id);
			if(start_id == -1)
				return occured;
			occured++;
			start_id += std.count("<|start|>user");
		}
	}
	
	truncate(num = 1)
	{
		for(i = 0; i < num; i++)
		{
			first_id = prompt.find("<|start|>user");
			
			if(first_id == -1)
				return;
			
			second_id = prompt.find("<|start|>user", first_id+std.count("<|start|>user"));
			
			if(second_id == -1)
				prompt = "";
			else
				prompt = prompt.substr(second_id);
		}
	}
	
	build_tools_prompt()
	{
		if(this.tools.size() == 0)
			return "";
		
		res = "\n# Tools\n";
		res += "## functions\n";
		res += "namespace functions {\n";
		res += tools.trim();
		res += "\n} // namespace functions";
		
		return res;
	}
}

agt_timestamp()
{
	ts = std.timestamp();
	ts = ts.substr(0, ts.rfind(":")); //remove seconds
	ts = ts.substr(0, ts.rfind(":")); //remove minutes
	ts += " o' clock.";
	return ts;
}

create_harmony_context()
{
	return harmony_context("");
}